InternalClassType: Method
Coordinate3D: [55.050000000000004,-38.25,0]
Name: CreateResultsReport
Origin: 098aa45a-2d0b-4a6f-aea3-c58937895564
Program: |+1
 param pResultsFolder:string
 /*
 1. Read_all files like "Exp2_Obs3.txt" in the folder pResultsFolder
 2. Fill the tablefile Experimentmanager.DetailedResults
 3. Statistical evaluation and preparation of the Report
 4. Save the report
 */
 var tab: table[string,string,string] -- Path of the Output Value, Data Type, Value
 var FileName, OutputStr:string;
 var ObsTab:table;
 var col:integer
 
 RescaleActive := true
 tab.create
 RescaleResultsFolder := pResultsFolder
 resetWizard
 for var ExpNo := 1 to ExpTable.yDim
 	evalExpTable(ExpNo)
 	prepExp(ExpNo) -- create subtables
 	for var ObsNo := 1 to NumRuns
 		FileName := to_str(RescaleResultsFolder,"\\\\Exp",ExpNo,"_Obs",ObsNo,".txt") -- like Exp211_Obs13.txt
 		if existsFile(FileName)
 			tab.readFile(FileName)
 			for var OutNo := 1 to tab.yDim
 				OutputStr := ValueDescriptions.get(tab[1,OutNo])
 				col := DetailedResults.getColumnNo(OutputStr)
 				ObsTab := DetailedResults[col + 6, ExpNo]
 				switch tab[2,OutNo]
 				case "time"
 					ObsTab[1,ObsNo] := str_to_time(tab[3,OutNo])
 				else
 					ObsTab[1,ObsNo] := str_to_num(tab[3,OutNo])
 				end
 			next -- OutNo
 		end
 	next -- ObsNo
 	CalcStatistics4Exp(ExpNo)
 next -- ExpNo
 
 --if Kind_Output.numberOfOutputValues > 0 then
 --	Dialog.Dialog.setSensitive("Chart_but", true);
 --end
 
 if Design4FactorialAnalysis
 	FactorialAnalysis(FactorDesignValue)
 end
 T_Test(pValues)
 
 --Dialog.Dialog.setSensitive("HTML_but", true); -- needed for using the dialog of the ExpMgr, not needed for this purpose
 
 -- open the Report
 -- ReportObject.SaveToDirectory := RescaleResultsFolder; -- needed for using the dialog of the ExpMgr, not needed for this purpose
 prepReport
 ReportObject.save(to_str(RescaleResultsFolder,"\\Report.htm"))
 
 RescaleActive := false
RandomSeed: 638
UUID: 9cb4a53b-0f3f-421f-bd63-cde2be00c3cc
