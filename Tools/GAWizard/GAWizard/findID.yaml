InternalClassType: Method
Coordinate3D: [11.55,-12,0]
Name: findID
Origin: a9e59983-6ee0-4391-a7f4-563f027af6cc
Program: |+1
 -- unique ID, which is the firstly occurred individual number
 -- called by:_alreadyEvaluated
 param ID:integer->integer
 
 	
 var individualTable:table := StatisticalEvaluation[0,ID]
 var m:integer := 199 -- number of columns of the hash table is a prime number
 var t:table
 var sepItem:string := ";"
 var sepChrom:string := "|"
 var descStr:string := sepChrom
 for var chromNo := 1 to individualTable.yDim 
 	t := individualTable[1,chromNo]
 	for var a := 1 to t.yDim-1 
 		descStr := to_str(descStr,t[1,a], sepItem)
 	next
 	descStr := to_str(descStr,t[1,t.yDim],sepChrom)
 next
 
 StatisticalEvaluation[8,ID] := descStr -- unique description
 
 var hashValue:integer := strHash(descStr)
 var hashReduced:integer := hashValue mod m + 1 -- range {1,...,m}
 
 HashTable.setCursor(hashReduced,1)
 while HashTable.find({hashReduced,1}..{hashReduced,*}, hashValue) 
 	var candID:integer := IDs[hashReduced,HashTable.CursorY]
 	if StatisticalEvaluation[8,candID] = descStr  -- comparison of the unique description
 		StatisticalEvaluation[7,ID] := candID -- register the duplicate			
 		return candID
 	end
 end
 
 var row:integer := HashTable[hashReduced,0] + 1
 
 HashTable[hashReduced,0] := row
 HashTable[hashReduced,row] := hashValue
 IDs[hashReduced,row] := ID
 
 return ID

RandomSeed: 396
UUID: 01e31c4f-ceba-4813-80b3-cb1ed72afdac
